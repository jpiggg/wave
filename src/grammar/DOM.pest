WHITESPACE = _{ " " | "\t" | "\n" | "\r" }
_QUOTE = _{ "\"" }
_left_chevron = _{ "<" }
_right_chevron = _{ ">" }
tag_name = @{(!(WHITESPACE | ">") ~ ANY)*}
attr_name = @{ASCII_ALPHA+}
attr_value = @{ (!"\"" ~ ANY)* }
tag_attr = {attr_name ~ "=" ~ "\"" ~ attr_value ~ "\"" }
_opening_tag = _{ _left_chevron ~ PUSH(tag_name) ~ tag_attr* ~ _right_chevron }
_closing_tag = _{ _left_chevron ~ "/" ~ POP ~ _right_chevron }
heading_level = {ASCII_NONZERO_DIGIT}
_heading_opening = _{ _left_chevron ~ "h" ~ PUSH(heading_level) ~ _right_chevron}
_heading_closing = _{_left_chevron ~ "/" ~ "h" ~ POP ~ _right_chevron}
heading = { _heading_opening ~ text_content* ~ _heading_closing }
_description_opening = _{_left_chevron ~ PUSH("desc") ~ _right_chevron}
_description_closing = _{_left_chevron ~ "/" ~ POP ~ _right_chevron}
description = {_description_opening ~ text_content ~ _description_closing}
_comment_start = _{_left_chevron ~ "!" ~ "--"}
comment = {(!_comment_end ~ ANY)*}
_comment_end = _{"--" ~ _right_chevron}
_comment_block = _{_comment_start ~ comment ~ _comment_end}
children = {_comment_block | heading | description | element | text_content}
text_content = {(!_left_chevron ~ ANY)+}
element = { _opening_tag ~ children* ~ _closing_tag }
eoi = _{ !ANY }
document = {SOI ~ (heading | element) ~ eoi}